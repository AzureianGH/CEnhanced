module Std.String;

bring Std.Memory as Mem;

expose fun strcmp(char* str1, char* str2) -> i32
{
    i32 idx = 0;
    char* c1 = str1;
    char* c2 = str2;
    while (c1[idx] != 0 && c2[idx] != 0)
    {
        if (c1[idx] != c2[idx])
        {
            ret (c1[idx] as i32) - (c2[idx] as i32);
        }
        idx++;
    }
    ret (c1[idx] as i32) - (c2[idx] as i32);
}

expose fun strlen(char* str) -> i32
{
    i32 len = 0;
    while (str[len] != 0)
    {
        len++;
    }
    ret len;
}

expose fun strcpy(char* dest, char* src) -> char*
{
    i32 idx = 0;
    while (src[idx] != 0)
    {
        dest[idx] = src[idx];
        idx++;
    }
    dest[idx] = 0; // Null-terminate
    ret dest;
}

expose fun strcat(char* dest, char* src) -> char*
{
    i32 dest_len = strlen(dest);
    i32 idx = 0;
    while (src[idx] != 0)
    {
        dest[dest_len + idx] = src[idx];
        idx++;
    }
    dest[dest_len + idx] = 0; // Null-terminate
    ret dest;
}

expose fun strchr(char* str, char ch) -> char*
{
    i32 idx = 0;
    while (str[idx] != 0)
    {
        if (str[idx] == ch)
        {
            ret (&str[idx]) as char*;
        }
        idx++;
    }
    ret 0 as char*;
}

expose fun strstr(char* haystack, char* needle) -> char*
{
    i32 hlen = strlen(haystack);
    i32 nlen = strlen(needle);
    if (nlen == 0)
    {
        ret haystack;
    }
    for (i32 i = 0; i <= hlen - nlen; i++)
    {
        i32 j = 0;
        while (j < nlen && haystack[i + j] == needle[j])
        {
            j++;
        }
        if (j == nlen)
        {
            ret (&haystack[i]) as char*;
        }
    }
    ret 0 as char*;
}

expose fun ends_with(char* str, char* suffix) -> i32
{
    i32 str_len = strlen(str);
    i32 suffix_len = strlen(suffix);
    if (suffix_len > str_len)
    {
        ret 0;
    }
    for (i32 i = 0; i < suffix_len; i++)
    {
        if (str[str_len - suffix_len + i] != suffix[i])
        {
            ret 0;
        }
    }
    ret 1;
}

expose fun strdup(char* str) -> char*
{
    i32 len = strlen(str);
    void* mem = Mem.alloc(len + 1);
    if (mem == 0 as void*)
    {
        ret 0 as char*;
    }
    char* dest = mem as char*;
    for (i32 i = 0; i < len; i++)
    {
        dest[i] = str[i];
    }
    dest[len] = 0; // Null-terminate
    ret dest;
}

expose fun strncmp(char* str1, char* str2, i32 n) -> i32
{
    i32 idx = 0;
    while (idx < n && str1[idx] != 0 && str2[idx] != 0)
    {
        if (str1[idx] != str2[idx])
        {
            ret (str1[idx] as i32) - (str2[idx] as i32);
        }
        idx++;
    }
    if (idx == n)
    {
        ret 0;
    }
    ret (str1[idx] as i32) - (str2[idx] as i32);
}