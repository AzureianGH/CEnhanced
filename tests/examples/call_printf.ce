#include "test.h"

extend from "C" i32 printf(char *, _vaargs_);
extend from "C" i32 write(int, char *, int);
extend from "C" i32 read(i32, char *, int);
extend from "C" void *malloc(int);
extend from "C" void free(void *);
extend from "C" void exit(int);
extend from "C" int test();

extend fun test_chance() -> i32;

alias string = char*;
alias ptr<T> = T*;

fun printwrapper() -> i32
{
    ret printf("Hello, %s! Number: %d\n", "CHance", 42);
}

enum Element
{
    ElementPart, // Auto 0
    ElementPart2, // Auto 1
    SetElement = 3 // 3
};

struct AnElement
{
    Element internalElement;
    Element* ptrToElement;
};

struct OtherElement
{
    i32 part;
    char* str;
    u64 bignumber;
    AnElement thisthing;
    AnElement* ptrToSmth;
};

struct SmallTest
{
    i32 testVar;
    u8 smallValue;
};

fun main() -> i32
{
    // allocate 128 bytes for input
    ptr<void> buf = malloc(128);

    // prompt the user
    write(1, "What's your name? > ", 20);

    // read from stdin (fd = 0), max 127 chars
    i32 len = read(0, buf as char *, 127);

    // test printwrapper result
    i32 printed = printwrapper();
    printf("Read %d bytes (printwrapper printed %d chars)\n", len, printed);

    // null-terminate the string safely
    string str = buf as string;
    if (len > 0)
    {
        if (str[len - 1] == '\n') str[--len] = 0;
        if (len > 0 && str[len - 1] == '\r') str[--len] = 0;
    }
    else
    {
        printf("No input read, using empty string\n");
        str[0] = 0;
    }

    // print greeting
    printf("Hello, %s!\n", str);

    printf("String length: %d\n", strlen(str));

    string cmp_target = "CHance";

    if (strcmp(str, cmp_target) == 0)
    {
        printf("You entered the secret name!\n");
    }
    else
    {
        printf("You did not enter the secret name.\n");
    }

    printf("Freeing memory...\n");
    free(buf);

    // Test overflow behavior
    i16 of = test_overflow();
    printf("Overflow test result (should be -25536): %d\n", of);

    printf("Calling test_chance(): %d\n", test_chance());

    printf("Calling extended C function test(): %d\n", test());

    printf("Multiplication test: 6 * 7 = %d\n", 6 * 7);

    printf("Multiplication overflow test (200 * 200 = 64): %d\n", test_mul_overflow());

    printf("Test hex output: %d in hex is %x\n", 0xFF, 0xFF);

    SmallTest vari = {};
    vari.smallValue = 3;
    vari.testVar = 44;
    
    SmallTest another = {
        .smallValue = 123,
        .testVar = 333 
    };

    SmallTest* test = &vari;

    Element elem = SetElement;

    test=>testVar = 45;

    if (test=>testVar == 45 || another.smallValue == 3)
    {
        printf("Logical OR worked!\n");
    }

    printf("typeof() = %s\n", typeof(ElementPart2));

    ret ((vari.testVar as i32) + (another.smallValue as i32)); // expect 45 + 123 = 168
}

fun construct_thing() -> SmallTest
{
    SmallTest thing = {};
    thing.smallValue = 5;
    thing.testVar = 10;
    ret thing;
}

fun strlen(string strg) -> i32
{
    i32 len = 0;
    while (strg[len])
    {
        len++;
    }
    ret len;
}

fun strcmp(string a, string b) -> i32
{
    i32 i = 0;
    while (a[i] && b[i])
    {
        if (a[i] != b[i])
        {
            ret a[i] - b[i];
        }
        i++;
    }
    ret a[i] - b[i];
}

fun abort() -> void
{
    exit(1);
    ret;
}

fun test_overflow() -> i16
{
    i16 x = 30000;
    i16 y = 10000;
    ret x + y; // expect overflow to -25536
}

fun test_mul_overflow() -> u8
{
    u8 a = 200;
    u8 b = 200;
    ret a * b; // expect overflow to 64 (200*200=40000 -> 40000 % 256 = 64)
}
